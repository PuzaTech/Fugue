apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "commons-cli:commons-cli:1.2"
    compile "com.google.code.gson:gson:2.3"
    compile "org.apache.logging.log4j:log4j-core:2.0.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }
}

jar {
    baseName = 'fugue-topicmodeling'
    version =  '0.1'
    manifest {
        attributes 'Main-Class': 'com.hongliangjie.fugue.topicmodeling.MainEntrance'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'The Fugue Topic Modeling Package',
                'Implementation-Version': version,
                'Main-Class': 'com.hongliangjie.fugue.topicmodeling.MainEntrance'
    }
    baseName = 'fugue-topicmodeling-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}